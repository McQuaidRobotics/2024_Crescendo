
// This script will update the markers in the .traj files in the src/main/deploy/choreo directory,
// it will take any toplevel named commands in the current event markers and clone themo over to events
// with the same name and timestamp

// make a gradle task to run this script
task updateTrajectories() {
    doLast {
        def deploy_directory = file('src/main/deploy/choreo')

        println "Updating markers in $deploy_directory"

        def slurper = new groovy.json.JsonSlurper()

        deploy_directory.eachFile { file ->
            println "    Updating $file"
            if (file.isFile() && file.name.endsWith('.traj')) {
                def data = slurper.parse(file)
                data['name'] = file.name - '.traj'

                if (data.containsKey('eventMarkers')) {
                    data['events'] = makeNewMarkers(data['eventMarkers'])
                } else {
                    data['events'] = []
                }

                file.text = new groovy.json.JsonBuilder(data).toPrettyString()
            }
        }
    }
}

def makeNewMarkers(oldMarkers) {
    def newMarkers = []
    oldMarkers.each { marker ->
        if (!marker.containsKey('command')) {
            return
        }
        def command = marker['command']
        if (!command.containsKey('type')) {
            return
        }
        if (command['type'] != 'named') {
            return
        }
        def newMarker = [
            timestamp: marker['timestamp'],
            event: command['data']['name']
        ]
        newMarkers << newMarker
    }
    return newMarkers
}

build.dependsOn updateTrajectories